managed implementation in class zbp_r_travel_kh unique;
strict ;
with draft;
define behavior for z_r_travel_kh alias Travel
persistent table ztravel_kh
draft table ztravel_khd
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( global, instance )

{
  create ;
  update;
  delete;

  // Campos automáticos
  field ( numbering : managed, readonly ) TravelUUID;
  //Campos de sólo lectura
  field ( readonly ) TravelID, OverallStatus, LocalCreatedAt, LastChangedAt, LocalCreatedBy, LocalLastChangedBy, TotalPrice;
  // Campos obligatorios
  field ( mandatory ) CustomerID, AgencyID, BeginDate, EndDate, CurrencyCode;

  field ( features : instance ) BookingFee;

  // Acciones
  action ( features : instance, authorization : global ) acceptTravel result [1] $self;
  action ( features : instance , authorization : global) rejectTravel result [1] $self;
  action ( features : instance, authorization : global ) DeductDiscount parameter Z_AE_TRAVEL_DISCOUNT_896 result [1] $self;

  internal action  reCaclTotalPrice;

  // Validaciones
  validation validateCustomer on save { create; field CustomerID; }
  validation validateAgency on save { create; field AgencyID; }
  validation validateDateRange on save { create; field BeginDate, EndDate; }

  //Determnación
  determination setTravelNumber on save { create; }
  determination setStatusToOpen on modify { create; }
  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  determine action validateCustomerID { validation validateCustomer; }
  determine action validateAgencyID { validation validateAgency; }
  determine action validateDates { validation validateDateRange; }

  side effects
  {
    field BookingFee affects field TotalPrice;
    determine action validateCustomerID executed on field CustomerID affects messages;
    determine action validateAgencyID executed on field AgencyID affects messages;
    determine action validateDates executed on field BeginDate, field EndDate affects messages;
  }

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare
  {

    validation validateCustomer;
    validation validateAgency;
    validation validateDateRange;
  }

  mapping for ztravel_kh
    {
      TravelUUID         = travel_uuid;
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}